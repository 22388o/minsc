use minis::ast::{Expr, Stmt, self};
use minis::util::concat;

grammar;

pub Program: Expr = Block;

// XXX can the ';' be optional?
Block: Expr = <stmts:(<Stmt> ";")*> <return_value:Expr> =>
  ast::Block { stmts,  return_value: return_value.into() }.into();

Stmt: Stmt = {
  FnDef,
  Assign,
}

Expr: Expr = {
  Call,
  And,
  Or,
  TermWord,
  BlockExpr,
};


Ident: String = <s:r"[\w$]+"> => (<>).to_owned();

TermWord: Expr = Ident => ast::TermWord(<>.into()).into();

Assign: Stmt = "let" <lhs:Ident> "=" <rhs:Expr> =>
    ast::Assign { lhs, rhs }.into();

FnDef: Stmt = {
    "fn" <ident:Ident> "(" <signature:List<Ident, ",">> ")" "=" <body:Expr> =>
        ast::FnDef { ident, signature, body }.into(),
    "fn" <ident:Ident> "(" <signature:List<Ident, ",">> ")" "{" <body:Block> "}" =>
        ast::FnDef { ident, signature, body }.into(),
};

Call: Expr = <ident:Ident> "(" <args:List<Expr, ",">> ")" =>
    ast::Call { ident, args }.into();

// TODO make the () optional
Or: Expr = "(" <ListM<Expr, "||">> ")" => ast::Or(<>).into();
And: Expr = "(" <ListM<Expr, "&&">> ")" => ast::And(<>).into();

BlockExpr: Expr = "{" <Block> "}" => <>.into();

// A `S`-separated list of *one* or more `T` values, *with* an optional suffix separator
List<T, S>: Vec<T> = <l:(<T> S)*> <t:T> S? => concat(l, t);

// A `S`-separated list of *two* or more `T` values, *without* an optional suffix separator
ListM<T, S>: Vec<T> = <l:(<T> S)+> <t:T> => concat(l, t);
