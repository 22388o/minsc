use minis::ast::Expr;
use minis::util::concat;

grammar;

pub Expr: Expr = {
  //Var,
  FnCall,
  FnDef,
  Assign,
  And,
  Or,
  Value,
  Block,
};

Ident: String = <s:r"[\w$]+"> => (<>).to_owned();

Value: Expr = Ident => Expr::Value(<>.into());

Assign: Expr = "let" <name:Ident> "=" <val:Expr> => Expr::Assign(name, val.into());

FnCall: Expr = <name:Ident> "(" <args:List<Expr, ",">> ")" => Expr::FnCall(name, args);

FnDef: Expr = {
  "fn" <name:Ident> "(" <args:List<Ident, ",">> ")" "=" <body:Expr> => Expr::FnDef(name, args, body.into()),
  "fn" <name:Ident> "(" <args:List<Ident, ",">> ")" <body:Block> => Expr::FnDef(name, args, body.into()),
};

// TODO make the () optional
Or: Expr = "(" <ListM<Expr, "||">> ")" => Expr::Or(<>);
And: Expr = "(" <ListM<Expr, "&&">> ")" => Expr::Or(<>);

// XXX can the ';' be optional?
Block: Expr = "{" <l:List<Expr, ";">> "}" => Expr::Block(l);

// A `S`-separated list of *one* or more `T` values, *with* an optional suffix separator
List<T, S>: Vec<T> = <l:(<T> S)*> <t:T> S? => concat(l, t);

// A `S`-separated list of *two* or more `T` values, *without* an optional suffix separator
ListM<T, S>: Vec<T> = <l:(<T> S)+> <t:T> => concat(l, t);




//Value: Expr = <s:r"\w+"> => Expr::Value(<>.into());
//Var: Expr = Ident => Expr::Var(<>.into());
